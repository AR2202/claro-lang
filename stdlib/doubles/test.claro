match (doubles::parseDouble(input("Give me a double to try parsing:"))) {
  case parsed:double -> print("Successfully parsed the given double: {parsed}");
  case ERR           -> print("Failed to parse: {ERR}");
}

print("Let's simulate a shitty calculator that can only do addition or subtraction of two terms...");
var inputExpr = input("Give me a single addition expression (e.g.: 1 + 1):");

atom ADD
atom SUBTRACT
newtype Expr : struct {
  lhs : double,
  op: oneof<ADD, SUBTRACT>,
  rhs : double
}

var tokens = strings::split(inputExpr, " ");
if (len(tokens) == 3) {
  var expr: oneof<Expr, std::Error<string>>;
  match ((doubles::parseDouble(tokens[0]), tokens[1], doubles::parseDouble(tokens[2]))) {
    case (lhs:double, "+", rhs:double) ->
      expr = Expr({lhs = lhs, op = ADD, rhs = rhs});
    case (lhs:double, "-", rhs:double) ->
      expr = Expr({lhs = lhs, op = SUBTRACT, rhs = rhs});
    case _ ->
      expr = std::Error("Error! Expected Expr in the format: <double> <+|-> <double>");
  }
  match (expr) {
    case ERR:std::Error<string> ->
      print(unwrap(ERR));
    case _:Expr ->
      match (unwrap(expr)) {
        case {lhs = lhs, op = _:ADD, rhs = rhs} ->
          print("= {lhs + rhs}");
        case {lhs = lhs, op = _:SUBTRACT, rhs = rhs} ->
          print("= {lhs - rhs}");
    }
  }
} else {
  print("Error! Expected 3 tokens separated by whitespace!");
}