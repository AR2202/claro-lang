
match (longs::parseLong(input("Give me a long to try parsing:"))) {
  case parsed:long -> print("Successfully parsed the given long: {parsed}");
  case ERR -> print("Failed to parse: {ERR}");
}

print("Let's simulate a shitty calculator that can only do addition or subtraction of two terms...");
var inputExpr = input("Give me a single addition expression (e.g.: 1 + 1):");

atom ADD
atom SUBTRACT
newtype Expr : struct {
  lhs : long,
  op: oneof<ADD, SUBTRACT>,
  rhs : long
}

var tokens = strings::split(inputExpr, " ");
if (len(tokens) == 3) {
  var expr: oneof<Expr, std::Error<string>>;
  match ((longs::parseLong(tokens[0]), tokens[1], longs::parseLong(tokens[2]))) {
    case (lhs:long, "+", rhs:long) ->
      expr = Expr({lhs = lhs, op = ADD, rhs = rhs});
    case (lhs:long, "-", rhs:long) ->
      expr = Expr({lhs = lhs, op = SUBTRACT, rhs = rhs});
    case _ ->
      expr = std::Error("Error! Expected Expr in the format: <long> <+|-> <long>");
  }
  match (expr) {
    case ERR:std::Error<string> ->
      print(unwrap(ERR));
    case _:Expr ->
      match (unwrap(expr)) {
        case {lhs = lhs, op = _:ADD, rhs = rhs} ->
          print("= {lhs + rhs}");
        case {lhs = lhs, op = _:SUBTRACT, rhs = rhs} ->
          print("= {lhs - rhs}");
    }
  }
} else {
  print("Error! Expected 3 tokens separated by whitespace!");
}