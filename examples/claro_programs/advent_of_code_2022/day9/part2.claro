# https://adventofcode.com/2022/day/9#part2

alias Dir : tuple<int, int>
alias Cmd : tuple<Dir, int>

provider getCmd() -> Cmd {
  var charToIntMap = {"0":0, "1":1, "2":2, "3":3, "4":4, "5":5, "6":6, "7":7, "8":8, "9":9};
  var charToDirMap = {"U": (-1, 0), "D": (1, 0), "L": (0, -1), "R": (0, 1)};
  var s = input();
  var res: Cmd = (charToDirMap[s[0]], 0);
  var i = 2;
  while (i < len(s)) {
    res[1] = res[1] * 10 + charToIntMap[s[i++]];
  }
  return res;
}

alias Point : tuple<int, int>
function areTouching(p1: Point, p2: Point) -> boolean {
  var abs: function<int -> int> = i -> i * (1 - numeric_bool(i < 0) * 2);
  return (abs(p1[0] - p2[0]) <= 1) and (abs(p1[1] - p2[1]) <= 1);
}

consumer move(rope: [Point], cmd: Cmd, tailVisitedPositions: {Point: int}) {
  while (cmd[1] > 0) {
    rope[0] = (rope[0][0] + cmd[0][0], rope[0][1] + cmd[0][1]);
    var i = 1;
    while (i < len(rope)) {
      if (not areTouching(rope[i-1], rope[i])) {
        var updateDir: function<|int, int| -> int> = lambda (a, b) -> {
          if ((a - b) > 0) {
            return b + 1;
          }
          return b - 1;
        };
        if (rope[i-1][0] == rope[i][0]) { # Vert
          rope[i] = (rope[i][0], updateDir(rope[i-1][1], rope[i][1]));
        } else if (rope[i-1][1] == rope[i][1]) { # Horiz
          rope[i] = (updateDir(rope[i-1][0], rope[i][0]), rope[i][1]);
        } else { # Diag
          rope[i] = (updateDir(rope[i-1][0], rope[i][0]), updateDir(rope[i-1][1], rope[i][1]));
        }
        if (i == len(rope) - 1) {
          tailVisitedPositions[rope[i]] = 1; # Wishlist: Again, wish Claro had mutable sets.
        }
      } else {
        # There was no movement at this point, so there's no more movement for the rest of the rope. Hack to `break;`.
        i = len(rope); # Wishlist: `break;`
      }
      ++i;
    }
    cmd[1] = cmd[1] - 1;
  }
}

# The rope starts entirely on top of itself.
var rope = [(0,0), (0,0), (0,0), (0,0), (0,0), (0,0), (0,0), (0,0), (0,0), (0,0)];
var tailVisitedPositions = {rope[len(rope) - 1]: 1}; # Wishlist: Again, wish Claro had mutable sets.
while (isInputReady()) {
  move(rope, getCmd(), tailVisitedPositions);
}
print("The tail took {len(tailVisitedPositions)} unique positions!");