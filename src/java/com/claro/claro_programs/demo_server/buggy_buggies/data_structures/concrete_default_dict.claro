newtype DefaultDict<DictTypes::K, DictTypes::V>: struct {
  defaultPr: provider<DictTypes::V>,
  dict: mut {DictTypes::K : DictTypes::V}
}

initializers DefaultDict {
  function getDefaultDict(defaultPr: provider<DictTypes::V>) -> DefaultDict<DictTypes::K, DictTypes::V> {
    return DefaultDict({defaultPr = defaultPr, dict = mut {}});
  }
}

# TODO(steving) Claro really needs to support overloading operator[] via some Contract.
unwrappers DefaultDict {
  function defaultDictGet<K,V>(d: DefaultDict<K,V>, key: K) -> V {
    var dict = unwrap(d).dict;
    if (not (key in dict)) {
      var defPr = unwrap(d).defaultPr; # TODO(steving) Tracking bug in: https://docs.google.com/spreadsheets/d/1PvMoLlIKfcq41F0tn0WRlBsamJ_gq_3dvEIKmoOlBqk/edit#gid=0&range=C7
      dict[key] = defPr();
    }
    return dict[key];
  }

  consumer defaultDictPut<K,V>(d: DefaultDict<K,V>, key: K, value: V) {
    var dict = unwrap(d).dict;
    dict[key] = value;
  }

  function asMap<K,V>(d: DefaultDict<K,V>) -> {K: V} {
    return copy(unwrap(d).dict);
  }
}
